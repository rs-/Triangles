(*

   Benedikt Ahrens and RÃ©gis Spadotti

   Coinitial semantics for redecoration of triangular matrices

   http://arxiv.org/abs/1401.1053

*)

(*

  Content of this file:

  definition of the category of setoids

*)

Require Import Theory.Category.
Require Import Theory.Product.

(*------------------------------------------------------------------------------
  -- ï¼£ï¼¡ï¼´ï¼¥ï¼§ï¼¯ï¼²ï¼¹  ï¼¯ï¼¦  ï¼³ï¼¥ï¼´ï¼¯ï¼©ï¼¤ï¼³
  ----------------------------------------------------------------------------*)
(** * Category of Setoids **)

(** In this file, we define the category of Setoids and show that this category has binary product.

    Note that to avoid universe inconsistancies we duplicate the definition of Setoid used to define
    the type of categories. **)

(** ** Setoid category definition **)

Module Setoids.

  Structure Obj := mkObj
  { SCarrier  :> Type
  ; SEquiv    : SCarrier â†’ SCarrier â†’ Prop
  ; is_SEquiv : Equivalence SEquiv }.

  Arguments mkObj  {_ _} _.
  Arguments SEquiv {_} _ _.

  Notation "'Setoids.make' â¦ƒ 'Carrier' â‰” c ; 'Equiv' â‰” eq â¦„" :=
    (@mkObj c eq _) (only parsing).

  Existing Instance is_SEquiv.

  Structure Morphism (A B : Obj) := mkMorphism
  { map  :> A â†’ B
  ; cong : âˆ€ {x y}, SEquiv x y â†’ SEquiv (map x) (map y) }.

  Instance map_Proper : âˆ€ A B (f : Morphism A B), Proper (SEquiv ==> SEquiv) (map A B f).
  Proof.
    intros A B f x y eq_xy.
    now apply cong.
  Qed.

  Arguments mkMorphism {_ _ _} _.
  Arguments map        {_ _} _ _.
  Arguments cong       {_ _} _ {_ _ _}.

  Module Morphism.

    Notation make map := (@mkMorphism _ _ map _) (only parsing).

  End Morphism.

  Program Definition Hom (A B : Obj) : Setoid :=
    Setoid.make â¦ƒ Carrier â‰” Morphism A B
                ; Equiv   â‰” Î» f g âˆ™ âˆ€ x y, SEquiv x y â†’ SEquiv (f x) (g y) â¦„.
  Next Obligation.
    constructor.
    - intros f x y eq_xy. now apply cong.
    - intros f g eq_fg x y eq_xy.
      etransitivity; [ apply cong; apply eq_xy | ].
      symmetry; now apply eq_fg.
    - intros f g h eq_fg eq_gh x y eq_xy.
      etransitivity; eauto. now apply eq_gh.
  Qed.

End Setoids.

Export Setoids.

Local Infix "â‡’" := Hom.

Program Definition id {A} : A â‡’ A := Setoids.Morphism.make (Î» x âˆ™ x).

Program Definition compose {A B C} : [ B â‡’ C âŸ¶ A â‡’ B âŸ¶ A â‡’ C ] :=
  Î» g f â†¦â‚‚ Setoids.Morphism.make (Î» x âˆ™ g (f x)).
Next Obligation.
  now do 2 apply cong.
Qed.
Next Obligation.
  intros fâ‚ fâ‚‚ eq_fâ‚fâ‚‚ gâ‚ gâ‚‚ eq_gâ‚gâ‚‚ x y eq_xy; simpl.
  now apply eq_fâ‚fâ‚‚, eq_gâ‚gâ‚‚.
Qed.

Local Infix "âˆ˜" := compose.

Lemma left_id A B (f : A â‡’ B) : id âˆ˜ f â‰ˆ f.
Proof.
  intros x y eq_xy; simpl; now apply cong.
Qed.

Lemma right_id A B (f : A â‡’ B) : f âˆ˜ id â‰ˆ f.
Proof.
  intros x y eq_xy; simpl; now apply cong.
Qed.

Lemma compose_assoc A B C D (f : A â‡’ B) (g : B â‡’ C) (h : C â‡’ D) : h âˆ˜ g âˆ˜ f â‰ˆ h âˆ˜ (g âˆ˜ f).
  intros x y eq_xy; simpl; now repeat apply cong.
Qed.

Canonical Structure ğ‘ºğ’†ğ’•ğ’ğ’Šğ’… : Category :=
  mkCategory left_id right_id compose_assoc.


(*------------------------------------------------------------------------------
  -- ï¼³ï¼¥ï¼´ï¼¯ï¼©ï¼¤ï¼³  ï¼¨ï¼¡ï¼¶ï¼¥  ï¼¢ï¼©ï¼®ï¼¡ï¼²ï¼¹  ï¼°ï¼²ï¼¯ï¼¤ï¼µï¼£ï¼´
  ----------------------------------------------------------------------------*)
(** ** Setoids have binary product **)

Section Product_construction.

  Infix "âˆ¼" := SEquiv (at level 70).

  Program Definition product (A B : ğ‘ºğ’†ğ’•ğ’ğ’Šğ’…) : ğ‘ºğ’†ğ’•ğ’ğ’Šğ’… :=
    Setoids.make â¦ƒ Carrier â‰” A âŸ¨Ã—âŸ© B
                 ; Equiv   â‰” Î» Sâ‚ Sâ‚‚ âˆ™ fst Sâ‚ âˆ¼ fst Sâ‚‚ âˆ§ snd Sâ‚ âˆ¼ snd Sâ‚‚ â¦„.
  Next Obligation.
    constructor; hnf.
    - intros [a  b]; split; reflexivity.
    - intros [x y] [x' y'] [eq_xx' eq_yy']; split; now symmetry.
    - intros [x y] [x' y'] [x'' y''] [eq_xx' eq_yy'] [eq_x'x'' eq_y'y''];
      split; etransitivity; eauto.
  Qed.

  Program Definition product_mor (A B C : ğ‘ºğ’†ğ’•ğ’ğ’Šğ’…) (f : C â‡’ A) (g : C â‡’ B) : C â‡’ product A B :=
    Setoids.Morphism.make (Î» c âˆ™ (f c , g c)).
  Next Obligation.
    split; now apply cong.
  Qed.

  Program Definition proj_l {A B} : product A B â‡’ A := Setoids.Morphism.make fst.

  Program Definition proj_r {A B} : product A B â‡’ B := Setoids.Morphism.make snd.

End Product_construction.


Program Instance ğ‘ºğ’†ğ’•ğ’ğ’Šğ’…_BinaryProduct : BinaryProduct ğ‘ºğ’†ğ’•ğ’ğ’Šğ’… :=
  BinaryProduct.make â¦ƒ Category â‰” ğ‘ºğ’†ğ’•ğ’ğ’Šğ’…
                     ; _Ã—_      â‰” product
                     ; âŸ¨_,_âŸ©    â‰” @product_mor _ _
                     ; Ï€â‚       â‰” proj_l
                     ; Ï€â‚‚       â‰” proj_r â¦„.
Next Obligation. (* Pmor_congâ‚‚ *)
  intros fâ‚ fâ‚‚ eq_fâ‚fâ‚‚ gâ‚ gâ‚‚ eq_gâ‚gâ‚‚ x y eq_xy; simpl; split.
  - now apply eq_fâ‚fâ‚‚.
  - now apply eq_gâ‚gâ‚‚.
Qed.
Next Obligation.
  now apply cong.
Qed.
Next Obligation.
  now apply cong.
Qed.
